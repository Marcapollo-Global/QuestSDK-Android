apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 4
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions{
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'

    // Http Rest Helper
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta1'
    // Retrofit Adapters
    compile 'com.squareup.retrofit:retrofit-adapters:2.0.0-beta1'
    // Retrofit converter-moshi
    compile 'com.squareup.retrofit:converter-moshi:2.0.0-beta1'
    // Deal with JSON and java OR mapping
    compile 'com.squareup.moshi:moshi:1.0.0'
    // Android beacon library
    compile 'org.altbeacon:android-beacon-library:2+'
}

// Specify POM details
group = 'com.marcapollo.questsdk' // Change this to match your package name
version = '1.0.4' // Change this to match your version number

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'maven'
        name = 'com.marcapollo.questsdk'

        version {
            name = '1.0.4'
            desc = 'Quest SDK 1.0.4'
            released  = new Date()
            vcsTag = '1.0.4'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/shine-chen/QuestSDK-Android'
        websiteUrl = 'https://github.com/shine-chen/QuestSDK-Android'
    }
    configurations = ['archives']
}